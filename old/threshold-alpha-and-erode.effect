// Alpha Channel Reflection Mask
// by Millennium Cyborg
// for use with OBS + StreamFX

// Always provided by OBS
uniform float4x4 ViewProj<
	bool automatic = true;
	string name = "View Projection Matrix";
>;

// Provided by Stream Effects
uniform texture2d InputA<
	bool automatic = true;
>;

uniform float4 ViewSize<
	bool automatic = true;
>;

// User values
uniform float _01_ThresholdLo<
  bool automatic = false;
	string name = "Lower Threshold";
	string description = "Ignore pixels with alpha below this value";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 1;
	float step = 0.01;
> = 0.01;

uniform float _02_ThresholdHi<
  bool automatic = false;
	string name = "Upper Threshold";
	string description = "Ignore pixels with alpha above this value";
	string field_type = "slider";
	float minimum = 0;
	float maximum = 1;
	float step = 0.01;
> = 0.99;

uniform float _03_MaskSize<
  bool automatic = false;
	string name = "Erode mask size";
	string description = "Get rid of regions smaller than this width";
	string field_type = "slider";
	float minimum = 1;
	float maximum = 9;
	float step = 1;
> = 3;

// ---------- Shader Code
sampler_state def_sampler {
	AddressU  = Clamp;
	AddressV  = Clamp;
	Filter    = Linear;
};

struct VertData {
	float4 pos : POSITION;
	float2 uv  : TEXCOORD0;
};

VertData VSDefault(VertData v_in) {
	VertData vert_out;
	vert_out.pos = mul(float4(v_in.pos.xyz, 1.0), ViewProj);
	vert_out.uv  = v_in.uv;
	return vert_out;
}

float threshold(float inp) {
  return ((_01_ThresholdLo < inp) && (inp < _02_ThresholdHi));
}

float4 PSDefault(VertData v_in) : TARGET {
  const float2 px_size = float2(1,1) / ViewSize.xy;
  const int half_size = floor(_03_MaskSize / 2);
  float a0 = 1.0;
  for (int i = -half_size; i <= half_size; ++i) {
    for (int j = -half_size; j <= half_size; ++j) {
      const float a2 = InputA.Sample(def_sampler, v_in.uv + float2(i * px_size.x, j * px_size.y)).a;
      const float a1 = threshold(a2);
      a0 = min(a0, a1);
    }
  }
  return float4(a0, a0, a0, 1.0);
}

technique Draw
{
	pass
	{
		vertex_shader = VSDefault(v_in);
		pixel_shader  = PSDefault(v_in); 
	}
}
